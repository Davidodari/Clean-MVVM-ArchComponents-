apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'jacoco-android'

apply plugin: 'io.fabric'

apply plugin: 'com.mikepenz.aboutlibraries.plugin'

jacoco {
    toolVersion = "0.8.4"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

jacocoAndroidUnitTestReport {
    xml.enabled true
}

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.k0d4black.theforce"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCodeMajor * 10000 + rootProject.ext.versionCodeMinor * 100 + rootProject.ext.versionCodePatch
        versionName rootProject.ext.versionName
        testInstrumentationRunner "com.k0d4black.theforce.runner.MockTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    dataBinding {
        enabled true
    }

    testOptions.unitTests {
        includeAndroidResources = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    bundle {
        density {
            enableSplit true
        }
        abi {
            enableSplit true
        }
        language {
            enableSplit false
        }
    }
}

dependencies {

    def appDependencies = rootProject.ext.appDependencies
    def testDependencies = rootProject.ext.testDependencies

    //Local Libs
    implementation project(':data')
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    implementation appDependencies.kotlin
    implementation appDependencies.appCompat
    implementation appDependencies.androidxKtx
    implementation appDependencies.constraintLayout
    implementation appDependencies.supportV4

    //Jetpack
    implementation appDependencies.fragmentKtx
    implementation appDependencies.lifecycleExtensions

    //Material
    implementation appDependencies.material

    //Firebase
    implementation appDependencies.firebaseAnalytics
    implementation appDependencies.crashlytics

    //Animations
    implementation appDependencies.recyclerviewAnimations

    //Memory leaks
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    debugImplementation appDependencies.leakCanary

    //About Libraries
    implementation appDependencies.aboutLibraryCore
    implementation appDependencies.aboutLibraryUi

    //DI
    implementation appDependencies.dagger2
    implementation appDependencies.dagger2Android
    kapt appDependencies.dagger2AndroidProcessor
    kapt appDependencies.dagger2Processor

    //Test Libs
    testImplementation testDependencies.junit
    testImplementation testDependencies.robolectric
    testImplementation testDependencies.androidxJunit
    testImplementation testDependencies.espressoCore
    testImplementation testDependencies.runner
    testImplementation testDependencies.rules
    testImplementation testDependencies.googleTruth
    testImplementation testDependencies.mockito
    testImplementation testDependencies.archComponentTest
    testImplementation testDependencies.coroutinesTest
    testImplementation testDependencies.mockitoInline
    androidTestImplementation testDependencies.espressoCore
    androidTestImplementation testDependencies.runner
    androidTestImplementation testDependencies.rules
    androidTestImplementation testDependencies.androidxJunit
    androidTestImplementation testDependencies.androidxCoreTest
    androidTestImplementation testDependencies.mockWebServer
    androidTestImplementation testDependencies.espressoContrib
    androidTestImplementation testDependencies.espressoIntents
    kaptAndroidTest testDependencies.dagger2Processor
    kaptAndroidTest appDependencies.dagger2AndroidProcessor
}
apply plugin: 'com.google.gms.google-services'
