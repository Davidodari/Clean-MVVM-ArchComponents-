/**
 *
 * Copyright 2020 David Odari
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License. You may obtain a copy of the License at
 *          http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the License
 * is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express
 * or implied. See the License for the specific language governing permissions and limitations under
 * the License.
 *
 **/
plugins {
    id("com.android.application")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id("com.google.firebase.crashlytics")
    id("com.mikepenz.aboutlibraries.plugin")
}

android {

    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "com.k0d4black.theforce"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode versionCodeMajor * 10000 + versionCodeMinor * 100 + versionCodePatch
        versionName versionName
        testInstrumentationRunner "com.k0d4black.theforce.runner.MockTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsEnabled: true]
        }

        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [crashlyticsEnabled: false]
            testCoverageEnabled true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions.unitTests {
        includeAndroidResources = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {

    // Features
    implementation project(':feature-about')
    implementation project(':feature-character-details')
    implementation project(':feature-character-search')
    implementation project(':feature-character-search-results')
    implementation project(':feature-favorite-characters')
    implementation project(':feature-home')
    implementation project(':feature-recent-character-search')
    implementation project(':feature-settings')
    implementation project(':shared-android')

    //Memory leaks
    debugImplementation appDependencies.leakCanary

    // Koin
    implementation appDependencies.koinAndroid
}
apply plugin: 'com.google.gms.google-services'
